!
! Distributed Linear Algebra with Future (DLAF)
!
! Copyright (c) ETH Zurich
! All rights reserved.
!
! Please, refer to the LICENSE file in the root directory.
! SPDX-License-Identifier: BSD-3-Clause
!

#:set precision = ['sp', 'dp']
#:set types = ['real', 'complex']
#:set names = {('sp', 'real'): 'ssy', ('sp', 'complex'): 'che', ('dp', 'real'): 'dsy', ('dp', 'complex'): 'zhe'}
#:set symbols = {('sp', 'real'): 's', ('sp', 'complex'): 'c', ('dp', 'real'): 'd', ('dp', 'complex'): 'z'}
module pxheevd_tests
   use iso_fortran_env, only: error_unit, sp => real32, dp => real64, i8 => int64
   use dlaf_fortran, only: dlaf_initialize, dlaf_finalize, dlaf_create_grid_from_blacs, dlaf_free_grid
   #:for dtype in precision
      #:for type in types
         #:set name = names[(dtype, type)]
         use dlaf_fortran, only: dlaf_p${name}$evd, dlaf_p${name}$evd_partial_spectrum
      #:endfor
   #:endfor

   use testutils, only: allclose, terminate, setup_mpi, teardown_mpi, bcast_check, set_random_matrix, init_desc

   implicit none

   external blacs_pinfo
   external blacs_get
   external blacs_gridinit
   external blas_gridinfo
   external blacs_gridexit
   external blacs_exit
   integer, external :: numroc

   #:for dtype in precision
      #:for type in types
         #:set name = names[(dtype, type)]
         public :: p${name}$evd_test
      #:endfor
   #:endfor

contains

   #:for dtype in precision
      #:for type in types
         #:set name = names[(dtype, type)]
         #:set symbol = symbols[(dtype, type)]
         subroutine p${name}$evd_test

            integer, parameter :: n = 4
            character, parameter :: uplo = 'L' ! NOTE: pxheevd limited to uplo='L'
            integer(kind=i8), parameter :: eval_idx_begin = 1
            integer(kind=i8), parameter :: eval_idx_end = 2

            integer:: nprow, npcol
            integer:: i, j

            logical :: failed
            integer :: rank, numprocs, myrow, mycol
            integer :: ictxt, ictxt_0
            integer :: info, lld, nb, ma, na
            integer :: desca(9), desca_local_dlaf(9), desca_local_scalapack(9)
            integer :: descz_local_dlaf(9), descz_local_scalapack(9), descz_local_dlaf_partial_spectrum(9)
            integer :: descz_dlaf(9), descz_scalapack(9), descz_dlaf_partial_spectrum(9)
            ${type}$ (kind=${dtype}$), dimension(:, :), allocatable :: A, A_local_dlaf, A_local_scalapack
            ${type}$ (kind=${dtype}$), dimension(:, :), allocatable :: A_local_dlaf_partial_spectrum
            ${type}$ (kind=${dtype}$), dimension(:, :), allocatable :: Z_local_dlaf, Z_local_scalapack
            ${type}$ (kind=${dtype}$), dimension(:, :), allocatable :: Z_local_dlaf_partial_spectrum
            ${type}$ (kind=${dtype}$), dimension(:, :), allocatable :: Z_dlaf, Z_scalapack
            ${type}$ (kind=${dtype}$), dimension(:, :), allocatable :: Z_dlaf_partial_spectrum
            real(kind=${dtype}$), dimension(:), allocatable :: W_dlaf, W_scalapack, W_dlaf_partial_spectrum
            real(kind=${dtype}$), parameter :: abstol = #{if dtype == 'sp'}# 1e-5_${dtype}$ #{else}# 1e-8_${dtype}$ #{endif}#

            integer, parameter :: lwork = 100, liwork = 100
            #:if type == 'complex'
               integer, parameter :: lrwork = 100
               real(kind=${dtype}$), dimension(lrwork) :: rwork
            #:endif
            ${type}$ (kind=${dtype}$), dimension(lwork) :: work
            integer, dimension(liwork) :: iwork

            nprow = 2
            npcol = 3
            nb = 2

            call setup_mpi(nprow, npcol, rank, numprocs)

            ! Setup BLACS
            call blacs_get(0, 0, ictxt)
            ictxt_0 = ictxt
            call blacs_gridinit(ictxt, 'R', nprow, npcol)
            call blacs_gridinit(ictxt_0, 'R', 1, 1)
            call blacs_pinfo(rank, numprocs)
            call blacs_gridinfo(ictxt, nprow, npcol, myrow, mycol)

            ! Setup full matrices on rank 0
            call init_desc(desca)
            call init_desc(descz_scalapack)
            call init_desc(descz_dlaf)
            call init_desc(descz_dlaf_partial_spectrum)
            if (rank == 0) then
               allocate (A(n, n))
               allocate (Z_dlaf(n, n))
               allocate (Z_scalapack(n, n))
               allocate (Z_dlaf_partial_spectrum(n, n))

               call descinit(desca, n, n, n, n, 0, 0, ictxt_0, n, info)
               call descinit(descz_dlaf, n, n, n, n, 0, 0, ictxt_0, n, info)
               call descinit(descz_scalapack, n, n, n, n, 0, 0, ictxt_0, n, info)
               call descinit(descz_dlaf_partial_spectrum, n, n, n, n, 0, 0, ictxt_0, n, info)

               call set_random_matrix(A)
            end if

            ! Allocate local matrices
            ma = numroc(n, nb, myrow, 0, nprow)
            na = numroc(n, nb, mycol, 0, npcol)
            lld = max(1, ma)
            allocate (A_local_dlaf(ma, na), A_local_scalapack(ma, na), A_local_dlaf_partial_spectrum(ma, na))
            allocate (Z_local_dlaf(ma, na), Z_local_scalapack(ma, na), Z_local_dlaf_partial_spectrum(ma, na))
            allocate (W_dlaf(n), W_scalapack(n), W_dlaf_partial_spectrum(n))

            ! + ---- +
            ! | DLAF |
            ! + ---- +

            call descinit(desca_local_dlaf, n, n, nb, nb, 0, 0, ictxt, lld, info)
            call p${symbol}$gemr2d(n, n, a, 1, 1, desca, A_local_dlaf, 1, 1, desca_local_dlaf, ictxt)
            call p${symbol}$gemr2d(n, n, a, 1, 1, desca, A_local_dlaf_partial_spectrum, 1, 1, desca_local_dlaf, ictxt)

            call descinit(descz_local_dlaf, n, n, nb, nb, 0, 0, ictxt, lld, info)
            call descinit(descz_local_dlaf_partial_spectrum, n, n, nb, nb, 0, 0, ictxt, lld, info)

            ! Solve with DLAF
            call dlaf_initialize()
            call dlaf_create_grid_from_blacs(ictxt)
            call dlaf_p${name}$evd( &
               uplo, &
               n, A_local_dlaf, 1, 1, desca_local_dlaf, &
               W_dlaf, Z_local_dlaf, 1, 1, descz_local_dlaf, &
               info &
               )
            call dlaf_p${name}$evd_partial_spectrum( &
               uplo, &
               n, A_local_dlaf_partial_spectrum, 1, 1, desca_local_dlaf, &
               W_dlaf_partial_spectrum, Z_local_dlaf_partial_spectrum, 1, 1, descz_local_dlaf_partial_spectrum, &
               eval_idx_begin, eval_idx_end, &
               info &
               )
            call dlaf_free_grid(ictxt)
            call dlaf_finalize()
            if (info /= 0) then
               write (error_unit, *) 'ERROR: dlaf_p${name}$evd returned info = ', info
               call terminate(ictxt)
            end if

            call p${symbol}$gemr2d(n, n, Z_local_dlaf, 1, 1, descz_local_dlaf, Z_dlaf, 1, 1, descz_dlaf, ictxt)
            call p${symbol}$gemr2d( &
                n, n, &
                Z_local_dlaf_partial_spectrum, 1, 1, descz_local_dlaf_partial_spectrum, &
                Z_dlaf_partial_spectrum, 1, 1, descz_dlaf_partial_spectrum, &
                ictxt &
            )

            ! + --------- +
            ! | ScaLAPACK |
            ! + --------- +

            call descinit(desca_local_scalapack, n, n, nb, nb, 0, 0, ictxt, lld, info)
            call descinit(descz_local_scalapack, n, n, nb, nb, 0, 0, ictxt, lld, info)
            call p${symbol}$gemr2d(n, n, a, 1, 1, desca, A_local_scalapack, 1, 1, desca_local_scalapack, ictxt)

            ! ScaLAPACK
            #:if type == 'real'
               call p${name}$evd( &
                  'V', uplo, &
                  n, A_local_scalapack, 1, 1, desca_local_scalapack, &
                  W_scalapack, Z_local_scalapack, 1, 1, descz_local_scalapack, &
                  work, lwork, iwork, liwork, info &
                  )
            #:else
               call p${name}$evd( &
                  'V', uplo, &
                  n, A_local_scalapack, 1, 1, desca_local_scalapack, &
                  W_scalapack, Z_local_scalapack, 1, 1, descz_local_scalapack, &
                  work, lwork, rwork, lrwork, iwork, liwork, info &
                  )
            #:endif
            if (info /= 0) then
               write (error_unit, *) 'ERROR: p${name}$evd returned info = ', info
               call terminate(ictxt)
            end if

            call p${symbol}$gemr2d(n, n, Z_local_scalapack, 1, 1, descz_local_scalapack, Z_scalapack, 1, 1, descz_scalapack, ictxt)

            ! + ------------- +
            ! | Check Results |
            ! + ------------- +
            ! Results are checked only on rank 0

            failed = .false.
            if (rank == 0) then
               if (.not. allclose(W_dlaf, W_scalapack)) then
                  failed = .true.
                  write (error_unit, *) "ERROR: DLAF != ScaLAPACK (eigenvalues)"
               end if

               if (.not. allclose(W_dlaf_partial_spectrum, W_scalapack)) then
                  failed = .true.
                  write (error_unit, *) "ERROR: DLAF_PS != ScaLAPACK (eigenvalues)"
               end if
            end if

            call bcast_check(failed)
            if (failed) then
               call terminate(ictxt)
            end if

            failed = .false.
            if (rank == 0) then
                if (.not. allclose(abs(Z_dlaf), abs(Z_scalapack), atol=abstol)) then
                    failed = .true.
                    write (error_unit, *) "ERROR: DLAF != ScaLAPACK (eigenvectors)"
                end if

                ! The first "eval_idx_end" eigenvectors are the same as ScaLAPACK
                if ( &
                    .not. allclose( &
                        abs(Z_dlaf_partial_spectrum(:,eval_idx_begin:eval_idx_end)), &
                        abs(Z_scalapack(:, eval_idx_begin:eval_idx_end)), &
                        atol=abstol) &
                    ) then
                    failed = .true.
                    write (error_unit, *) "ERROR: DLAF_PS != ScaLAPACK (eigenvectors [begin,end])"
                end if

                ! Something is off if the lastt eigenvectors are the same as ScaLAPACK
                if ( &
                    allclose( &
                        abs(Z_dlaf_partial_spectrum(:,eval_idx_end+1:n)), &
                        abs(Z_scalapack(:,eval_idx_end+1:n)), &
                        atol=abstol) &
                    ) then
                    failed = .true.
                    write (error_unit, *) "ERROR: DLAF_PS == ScaLAPACK (eigenvectors [end+1:N])"
                end if
            end if

            call bcast_check(failed)
            if (failed) then
               call terminate(ictxt)
            end if

            ! Cleanup

            if (rank == 0) then
               if (allocated(A)) deallocate (A)
               if (allocated(Z_dlaf)) deallocate (Z_dlaf)
               if (allocated(Z_scalapack)) deallocate (Z_scalapack)
            end if
            if (allocated(A_local_dlaf)) deallocate (A_local_dlaf)
            if (allocated(A_local_scalapack)) deallocate (A_local_scalapack)
            if (allocated(Z_local_dlaf)) deallocate (Z_local_dlaf)
            if (allocated(Z_local_scalapack)) deallocate (Z_local_scalapack)
            if (allocated(W_dlaf)) deallocate (W_dlaf)
            if (allocated(W_scalapack)) deallocate (W_scalapack)

            call blacs_gridexit(ictxt)
            call blacs_exit(1)
            call teardown_mpi()
         end subroutine p${name}$evd_test

      #:endfor
   #:endfor
end module pxheevd_tests
